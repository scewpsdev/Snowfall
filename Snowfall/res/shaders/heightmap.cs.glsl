#version 460


#define CHUNK_SIZE 32


layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

/*
layout(std430, set = 1, binding = 0) writeonly buffer OutData {
	float outData[];
};
*/

layout(set = 1, binding = 0, r32f) uniform image2D heightmapImg;

layout(std140, set = 2, binding = 0) uniform Params {
	ivec4 chunkPositionScale;
	vec4 octaveParams;

#define chunkPosition chunkPositionScale.xyz
#define chunkScale chunkPositionScale.w
#define baseFrequency octaveParams.x
#define frequencyMultiplier octaveParams.y
#define amplitudeMultiplier octaveParams.z
#define numOctaves octaveParams.w
};


vec3 permute(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

float simplex2d(vec2 v){
	const vec4 C = vec4(0.211324865405187, 0.366025403784439,
	         -0.577350269189626, 0.024390243902439);
	vec2 i  = floor(v + dot(v, C.yy) );
	vec2 x0 = v -   i + dot(i, C.xx);
	vec2 i1;
	i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
	vec4 x12 = x0.xyxy + C.xxzz;
	x12.xy -= i1;
	i = mod(i, 289.0);
	vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
	+ i.x + vec3(0.0, i1.x, 1.0 ));
	vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
	  dot(x12.zw,x12.zw)), 0.0);
	m = m*m ;
	m = m*m ;
	vec3 x = 2.0 * fract(p * C.www) - 1.0;
	vec3 h = abs(x) - 0.5;
	vec3 ox = floor(x + 0.5);
	vec3 a0 = x - ox;
	m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
	vec3 g;
	g.x  = a0.x  * x0.x  + h.x  * x0.y;
	g.yz = a0.yz * x12.xz + h.yz * x12.yw;
	return 130.0 * dot(m, g);
}

float fbm(vec2 x)
{
	float v = 0.0;
	float a = 0.5;
	vec2 shift = vec2(100);
	for (int i = 0; i < numOctaves; ++i) {
		v += a * simplex2d(x);
		x = x * frequencyMultiplier + shift;
		a *= amplitudeMultiplier;
	}
	return v;
}

void main()
{
	ivec3 gid = ivec3(gl_GlobalInvocationID);

	ivec2 worldPosition = chunkPosition.xz + gid.xy * chunkScale;

	float noise = fbm(worldPosition * baseFrequency);
	float height = noise * 100;

	imageStore(heightmapImg, gid.xy, vec4(height, 0, 0, 0));

	//uint idx = gid.x + gid.y * CHUNK_SIZE;
	//outData[idx] = height;
}
