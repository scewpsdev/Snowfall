#version 460


#define CHUNK_SIZE 32


layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in; // 8*8*8 = 512 threads

layout(set = 0, binding = 0) uniform usampler3D voxelData;

layout(std430, set = 1, binding = 0) writeonly buffer MaskData {
	uint faceMasks[];
};


void main()
{
	ivec3 gid = ivec3(gl_GlobalInvocationID);
	uint idx = gid.x + gid.y * CHUNK_SIZE + gid.z * CHUNK_SIZE * CHUNK_SIZE;

	uint block = texelFetch(voxelData, gid, 0).r;
	if (block == 0)
	{
		faceMasks[idx] = 0;
		return;
	}

#define FACE_X_NEG (1 << 0)
#define FACE_X_POS (1 << 1)
#define FACE_Y_NEG (1 << 2)
#define FACE_Y_POS (1 << 3)
#define FACE_Z_NEG (1 << 4)
#define FACE_Z_POS (1 << 5)

	uint faceMask = 0;

	if (gid.x == 0 || texelFetch(voxelData, gid + ivec3(-1, 0, 0), 0).r == 0) faceMask |= FACE_X_NEG;
	if (gid.x == CHUNK_SIZE - 1 || texelFetch(voxelData, gid + ivec3(1, 0, 0), 0).r == 0) faceMask |= FACE_X_POS;
	if (gid.y == 0 || texelFetch(voxelData, gid + ivec3(0, -1, 0), 0).r == 0) faceMask |= FACE_Y_NEG;
	if (gid.y == CHUNK_SIZE - 1 || texelFetch(voxelData, gid + ivec3(0, 1, 0), 0).r == 0) faceMask |= FACE_Y_POS;
	if (gid.z == 0 || texelFetch(voxelData, gid + ivec3(0, 0, -1), 0).r == 0) faceMask |= FACE_Z_NEG;
	if (gid.z == CHUNK_SIZE - 1 || texelFetch(voxelData, gid + ivec3(0, 0, 1), 0).r == 0) faceMask |= FACE_Z_POS;

	uint packed = (block & 0xFF) | faceMask << 8;
	faceMasks[idx] = packed;
}
